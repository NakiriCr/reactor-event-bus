apply plugin: 'java'
apply plugin: 'maven-publish'
apply plugin: 'signing'

apply from: 'artifact.gradle'

group 'cn.yanzx-dev.gitlab'
version artifactVersion

sourceCompatibility = 1.8

repositories {
    mavenCentral()
}

tasks.withType(JavaCompile) {
    options.encoding = 'UTF-8'
    options.compilerArgs << "-parameters"
}

configurations {
    all*.exclude group: 'org.slf4j', module: 'slf4j-log4j12'
    compileOnly {
        extendsFrom annotationProcessor
    }
}

dependencies {
    compile group: 'io.projectreactor', name: 'reactor-core', version: '3.2.0.RELEASE'

    annotationProcessor group: 'org.projectlombok', name: 'lombok', version: '1.18.4'
    testAnnotationProcessor 'org.projectlombok:lombok:1.18.2'
    testCompileOnly 'org.projectlombok:lombok:1.18.2'
    testCompile group: 'junit', name: 'junit', version: '4.12'
    testCompile group: 'io.projectreactor', name: 'reactor-test', version: '3.2.0.RELEASE'
}

task sourcesJar(type: Jar) {
    from sourceSets.main.allJava
    classifier = 'sources'
}

task javadocJar(type: Jar) {
    from javadoc
    classifier = 'javadoc'
}

tasks.withType(Javadoc) {
    options.addStringOption('Xdoclint:none', '-quiet')
    options.addStringOption('encoding', 'UTF-8')
    options.addStringOption('charSet', 'UTF-8')
}

publishing {
    publications {
        mavenJava(MavenPublication) {
            groupId = 'com.github.virtualcry'
            artifactId = 'reactor-event-bus'
            version = artifactVersion
            from components.java
            artifact sourcesJar
            artifact javadocJar
            pom {
                // artifact name
                name = 'Reactor Event Bus'
                // artifact description
                description = 'Reactor Event Bus'
                // artifact homepage
                url = 'https://github.com/virtualcry/reactor-event-bus'
                // licenses
                licenses {
                    license {
                        name = 'The Apache License, Version 2.0'
                        url = 'http://www.apache.org/licenses/LICENSE-2.0.txt'
                    }
                }
                // developers info
                developers {
                    developer {
                        name = 'VirtualCry'
                        email = '172519267@qq.com'
                    }
                }
                // scm
                scm {
                    url = 'https://github.com/virtualcry/reactor-event-bus'
                    connection = 'scm:git:https://github.com/virtualcry/reactor-event-bus.git'
                    developerConnection = 'scm:git:https://github.com/virtualcry/reactor-event-bus.git'
                }
            }
        }
    }
    // repositories to publish
    repositories {
        maven {
            def releasesRepoUrl = "https://oss.sonatype.org/service/local/staging/deploy/maven2/"
            def snapshotsRepoUrl = "https://oss.sonatype.org/content/repositories/snapshots/"
            url = version.endsWith('SNAPSHOT') ? snapshotsRepoUrl : releasesRepoUrl
            credentials {
                username sonatypeUsername
                password sonatypePassword
            }
        }
    }
}

signing {
    sign publishing.publications.mavenJava
}

jar {
    manifest {
        attributes 'Built-By': 'VirtualCry'
        attributes 'Created-By': 'Gradle-' + gradle.getGradleVersion()
        attributes 'Build-Jdk': java.getTargetCompatibility()
    }
}
